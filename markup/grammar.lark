// STARTING RULES
// ==============
start: element+
?element: full_element
    | self_closing_element

// ELEMENTS
// ========
full_element: _op_tag element_body _cl_tag
self_closing_element: "<" component attributes "/>"

element_body: (element | atom)*

atom: ESCAPED_STRING
    | NUMBER


// IMPORTS
// =======
?dotted_name: CNAME -> str
    | dotted_name "." CNAME

import_path: dotted_name [":" dotted_name]
    | "$" dotted_name ":" dotted_name -> import_absolute_path

component: import_path

// TAGS
// ====
_op_tag: "<" component attributes">"
_cl_tag: "</" component ">"

// ATTRIBUTES
// ==========
attributes: (raw_attribute | importable_attribute)*
?raw_attribute: CNAME "=" attribute_value
?importable_attribute: "@" CNAME "=" import_path

?attribute_value: atom

// LARK IMPORTS
// ==========

%import common.CNAME
%import common.ESCAPED_STRING
%import common.WS
%import common.NUMBER
%ignore WS